plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id "com.gorylenko.gradle-git-properties" version "2.2.3"
    id "org.sonarqube" version "3.0"
    id 'com.google.protobuf' version "0.8.13"
}

repositories {
    mavenCentral()
}


version = '1.3.6'
group = 'net.geant.nmaas'

sourceCompatibility = 1.8
targetCompatibility = 1.8

//buildscript {
//    repositories {
//        mavenCentral()
//        maven {
//            url "https://plugins.gradle.org/m2/"
//        }
//    }
//    dependencies {
//        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.16.RELEASE'
//        classpath 'gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.5.1'
//        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2'
//        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
//    }
//}

gitProperties {
    gitPropertiesDir = "src/main/resources"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.13.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.31.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

bootJar {
    mainClassName = 'net.geant.nmaas.MainConfig'
}

sourceSets {
    integrationTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly

    compileOnly {
        extendsFrom annotationProcessor
    }

    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    integrationTestCompileOnly 'org.projectlombok:lombok'
    integrationTestAnnotationProcessor 'org.projectlombok:lombok'

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-mail')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-devtools')
    implementation('org.springframework.boot:spring-boot-starter-quartz')
    implementation('org.springframework.boot:spring-boot-starter-log4j2')
    implementation('org.springframework.boot:spring-boot-starter-validation')

    implementation('org.modelmapper:modelmapper:2.2.0')
    implementation('org.modelmapper.extensions:modelmapper-spring:2.2.0')
    implementation('org.apache.commons:commons-lang3:3.9')
    implementation('commons-io:commons-io:2.6')

    runtimeOnly('com.h2database:h2')
    runtimeOnly('org.postgresql:postgresql')

    implementation('org.hibernate:hibernate-envers')
    implementation('io.jsonwebtoken:jjwt:0.9.1')
    implementation('org.freemarker:freemarker:2.3.30')
    implementation('com.hierynomus:sshj:0.21.1')
    implementation('org.gitlab4j:gitlab4j-api:4.14.27')
    implementation('com.google.guava:guava:24.1-jre')

    implementation('io.springfox:springfox-swagger2:2.8.0')
    implementation('io.springfox:springfox-swagger-ui:2.8.0')
    implementation('org.projectlombok:lombok:1.18.2')
    implementation('org.flywaydb:flyway-core:5.2.0')
    implementation('com.google.protobuf:protobuf-java:3.13.0')
    implementation('io.grpc:grpc-netty-shaded:1.31.1')
    implementation('io.grpc:grpc-protobuf:1.31.1')
    implementation('io.grpc:grpc-stub:1.31.1')
    implementation('io.fabric8:kubernetes-client:4.10.2')

	testImplementation('org.junit.jupiter:junit-jupiter-api:5.4.0')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.4.0')
	testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.4.0')
	testRuntimeOnly('org.junit.jupiter:junit-jupiter-migrationsupport:5.4.0')
    testImplementation('org.junit.platform:junit-platform-commons:1.4.0')
    testImplementation("org.mockito:mockito-core:2.24.5")
    testImplementation('org.mockito:mockito-junit-jupiter:2.24.5')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')

	// https://mvnrepository.com/artifact/io.fabric8/kubernetes-server-mock
    testImplementation group: 'io.fabric8', name: 'kubernetes-server-mock', version: '4.10.2'
	// https://mvnrepository.com/artifact/io.fabric8/servicecatalog-server-mock
    testImplementation group: 'io.fabric8', name: 'servicecatalog-server-mock', version: '4.10.2'


	integrationTestImplementation('org.junit.jupiter:junit-jupiter-api:5.4.0')
    integrationTestImplementation('org.junit.jupiter:junit-jupiter-params:5.4.0')
	integrationTestRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.4.0')
	integrationTestRuntimeOnly('org.junit.jupiter:junit-jupiter-migrationsupport:5.4.0')
    integrationTestImplementation('org.junit.platform:junit-platform-commons:1.4.0')
    integrationTestImplementation("org.mockito:mockito-core:2.24.5")
    integrationTestImplementation('org.mockito:mockito-junit-jupiter:2.24.5')
    integrationTestImplementation('org.springframework.security:spring-security-test')

	// https://mvnrepository.com/artifact/io.fabric8/kubernetes-server-mock
    integrationTestImplementation group: 'io.fabric8', name: 'kubernetes-server-mock', version: '4.10.2'
	// https://mvnrepository.com/artifact/io.fabric8/servicecatalog-server-mock
    integrationTestImplementation group: 'io.fabric8', name: 'servicecatalog-server-mock', version: '4.10.2'

}

test {
    useJUnitPlatform()
    systemProperties 'property': 'value'
    ignoreFailures = true
    afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
    jacoco {
        destinationFile = file("$buildDir/jacoco/test.exec")
        excludes = [
                '**/inventory/janitor/**',
                '**/exception/**',
                '**/exceptions/**'
        ]
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    afterTest { desc, result ->
        println "Executing itest ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
    dependsOn(test)
}

task integrationTestOnly(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    afterTest { desc, result ->
        println "Executing itest ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
    executionData(test, integrationTest)

}

jacocoTestCoverageVerification {
    executionData(test, integrationTest)
    violationRules {
        rule {
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.6
            }
        }
        rule {
            element = 'PACKAGE'
            includes = [
                    'net.geant.nmaas.portal.**'
            ]
            excludes = [
                    '**.model*'
            ]
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
        }
        rule {
            element = 'PACKAGE'
            includes = [
                    'net.geant.nmaas.orchestration.**'
            ]
            excludes = [
                    '**.model*'
            ]
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.3
            }
        }
    }
}

integrationTest.finalizedBy(jacocoTestCoverageVerification)

sonarqube {
    properties {
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec,${project.buildDir}/jacoco/integrationTest.exec"
    }
}
