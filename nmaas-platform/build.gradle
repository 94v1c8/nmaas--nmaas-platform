apply from: '../reactor/build.gradle'
apply plugin: 'org.springframework.boot'

buildscript {
	ext {
//		springBootVersion = '1.4.2.RELEASE'
//		dockerClientVersion = '7.0.2'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath('org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE')
	}
}

jar {
	baseName = 'nmaas-platform'
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('com.spotify:docker-client:7.0.2')
	compile('org.freemarker:freemarker:2.3.20')
	compile('com.hierynomus:sshj:0.20.0')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test:4.2.1.RELEASE')
}

task integrationTest(type: Test) {
	testClassesDir = sourceSets.integrationTest.output.classesDir
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
}