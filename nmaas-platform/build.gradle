apply from: '../reactor/build.gradle'
apply plugin: 'org.springframework.boot'

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath('org.springframework.boot:spring-boot-gradle-plugin:1.5.10.RELEASE')
	}
}

jar {
	baseName = 'nmaas-platform'
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')

	compile('org.modelmapper:modelmapper:1.1.0')
	compile('org.modelmapper.extensions:modelmapper-spring:1.1.0')
	
	runtime('com.h2database:h2')
	runtime('mysql:mysql-connector-java')
	runtime('org.postgresql:postgresql')
	
	compile('io.jsonwebtoken:jjwt:0.7.0')
	compile('com.spotify:docker-client:8.8.0')
	compile('org.freemarker:freemarker:2.3.20')
	compile('com.hierynomus:sshj:0.21.1')
	compile('org.gitlab4j:gitlab4j-api:4.6.9')
	compile('io.fabric8:kubernetes-client:3.1.8')

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test:4.2.1.RELEASE')
}

task integrationTest(type: Test) {
	testClassesDir = sourceSets.integrationTest.output.classesDir
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
	afterTest { desc, result ->
		println "Executing itest ${desc.name} [${desc.className}] with result: ${result.resultType}"
	}
}

jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.destination "${buildDir}/reports/jacoco/jacocoHtml"
	}
	executionData(test, integrationTest)
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test