apply from: '../reactor/build.gradle'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'org.sonarqube'
apply plugin: 'com.google.protobuf'

buildscript {
	repositories {
		mavenCentral()
		maven{
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath('org.springframework.boot:spring-boot-gradle-plugin:2.1.1.RELEASE')
		classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.5.1"
		classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
	}
}

gitProperties {
	gitPropertiesDir = "src/main/resources"
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.4.0"
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.17.1'
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

bootJar {
	mainClassName = 'net.geant.nmaas.MainConfig'
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
		exclude group: 'ch.qos.logback', module: 'logback-classic'
	}
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-devtools')
	compile('org.springframework.boot:spring-boot-starter-quartz')
	compile('org.springframework.boot:spring-boot-starter-log4j2')

	compile('org.modelmapper:modelmapper:2.2.0')
	compile('org.modelmapper.extensions:modelmapper-spring:2.2.0')
	
	runtime('com.h2database:h2')
	runtime('org.postgresql:postgresql')

	compile('org.hibernate:hibernate-envers')
	compile('io.jsonwebtoken:jjwt:0.7.0')
	compile('com.spotify:docker-client:8.8.0')
	compile('org.freemarker:freemarker:2.3.28')
	compile('com.hierynomus:sshj:0.21.1')
	compile('org.gitlab4j:gitlab4j-api:4.8.30')
	compile('com.google.guava:guava:23.0')

	compile('io.springfox:springfox-swagger2:2.8.0')
	compile('io.springfox:springfox-swagger-ui:2.8.0')
	compile ('org.projectlombok:lombok:1.18.2')
	compile ("org.flywaydb:flyway-core:5.2.0")
	compile ('com.google.protobuf:protobuf-java:3.6.0')
	compile ('io.grpc:grpc-netty-shaded:1.17.1')
	compile ('io.grpc:grpc-protobuf:1.17.1')
	compile ('io.grpc:grpc-stub:1.17.1')

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
}

test {
	systemProperties 'property': 'value'
	ignoreFailures = true
	afterTest { desc, result ->
		println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
	}
	jacoco {
		append = true
		destinationFile = file("$buildDir/jacoco/test.exec")
	}
}

task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
	afterTest { desc, result ->
		println "Executing itest ${desc.name} [${desc.className}] with result: ${result.resultType}"
	}
	dependsOn(test)
}

jacoco {
	toolVersion = "0.8.1"
	reportsDir = file("$buildDir/jacoco")
}

jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("${buildDir}/jacocoHtml")
	}
	executionData(test, integrationTest)
}

sonarqube {
	properties {
		property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec,${project.buildDir}/jacoco/integrationTest.exec"
	}
}
